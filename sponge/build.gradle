plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'net.kyori.blossom' version '1.2.0'
}

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    implementation project(':api')
    implementation project(':common')

    compileOnly ('org.spongepowered:spongeapi:7.3.0') {
        exclude(module: 'caffeine')
    }
    annotationProcessor 'org.spongepowered:spongeapi:7.3.0'
    implementation ('net.kyori:adventure-platform-spongeapi:4.0.0-SNAPSHOT') {
        exclude(module: 'checker-qual')
        exclude(module: 'guava')
    }
}

blossom {
    replaceTokenIn('src/main/java/me/syldium/thimble/sponge/ThSpongePlugin.java')
    replaceToken '@description@', project.description
    replaceToken '@version@', project.ext.fullVersion
    replaceToken '@url@', "https://github.com/syldium/Thimble"
}

shadowJar {
    archiveBaseName = "${project.parent.name}-Sponge"
    archiveClassifier = ''
    archiveVersion = project.ext.fullVersion

    dependencies {
        exclude(dependency('org.checkerframework:checker-qual'))
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'net.kyori.adventure', 'me.syldium.thimble.lib.adventure'
    relocate 'net.kyori.examination', 'me.syldium.thimble.lib.examination'
    minimize()
}

artifacts {
    archives shadowJar
}
