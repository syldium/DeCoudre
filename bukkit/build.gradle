plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net' }
}

dependencies {
    implementation project(':api')
    implementation project(':common')
    implementation project(':bukkit:integration')

    compileOnly ('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT') {
        exclude(module: 'json-simple')
    }
    compileOnly 'com.destroystokyo.paper:paper-mojangapi:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'it.unimi.dsi:fastutil:8.4.4'
    implementation ('net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT') {
        exclude(module: 'checker-qual')
        exclude(module: 'guava')
    }
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

processResources {
    filesMatching("plugin.yml") {
        expand(
                'pluginVersion': project.ext.fullVersion,
                'description': project.properties.description
        )
    }
}

shadowJar {
    archiveBaseName = "${project.parent.name}-Bukkit"
    archiveClassifier = ''
    archiveVersion = project.ext.fullVersion

    dependencies {
        exclude(dependency('org.checkerframework:checker-qual'))
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'net.kyori.adventure', 'me.syldium.thimble.lib.adventure'
    relocate 'net.kyori.examination', 'me.syldium.thimble.lib.examination'
    relocate 'org.bstats', 'me.syldium.thimble.lib.metrics'
    minimize()
}

artifacts {
    archives shadowJar
}
