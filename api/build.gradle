apply plugin: 'java-library'
apply plugin: 'maven-publish'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.spongepowered.org/maven' }
}

dependencies {
    api 'org.jetbrains:annotations:20.1.0'
    api 'net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT'

    compileOnly ('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT') {
        exclude(module: 'bungeecord-chat')
        exclude(module: 'guava')
        exclude(module: 'json-simple')
    }
    compileOnly ('org.spongepowered:spongeapi:7.3.0') {
        exclude(module: 'caffeine')
        exclude(module: 'guava')
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/syldium/Thimble")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'me.syldium'
            artifactId = 'thimble-api'
            version = project.ext.fullVersion

            pom {
                licenses {
                    license {
                        name = 'GNU General Public License (GPL) version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
            }

            from components.java
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
}

